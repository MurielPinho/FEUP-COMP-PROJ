options
{
    LOOKAHEAD=1;
}

PARSER_BEGIN(Parser)

import java.util.*;
import java.io.*;

import java.io.StringReader;

import pt.up.fe.comp.jmm.report.*;

public class Parser {
    private int countWhileExpressionParetheses = 0;
    private int whileExpressionErrorsCounter = 0;
    private int MAX_WHILE_EXPRESSION_ERRORS = 300;
    private ArrayList<Report> errorMessages = new ArrayList<Report>();

    /*public Parser(String file) throws ParseException, FileNotFoundException{
        // System.out.println("Parsing...");

        System.setIn(new FileInputStream(file));
		Parser parser = new Parser(System.in);
		//SimpleNode root = parser.Program();  // returns reference to root node

		//root.dump(""); // prints the tree on the screen
    }*/

    /* Skips the while expression until if finds the token "{" */
    public void skipWhileExpression() {
        Token prev; // will store the previous token
        int parenthesisCounter = 0;

        do{
            prev = token;
            token = getNextToken();

            if(token.kind == LPAR) countWhileExpressionParetheses++;
            else if(token.kind == RPAR) countWhileExpressionParetheses--;
        } while(token.kind != LBRACKET && token.kind != EOF && countWhileExpressionParetheses != 0);

        if(token.kind == LBRACKET)
            token = prev;
    }

    /* Handles with while expression error */
    public void handleWhileExpressionError(ParseException exception) throws ParseException {
        whileExpressionErrorsCounter++;
        
        if(whileExpressionErrorsCounter > this.MAX_WHILE_EXPRESSION_ERRORS) {
            System.out.println("Maximum Number of While Expression Errors(" + this.MAX_WHILE_EXPRESSION_ERRORS + ") exceeded.");
            this.printErrorMessages();
            throw new ParseException();
        }
        else this.buildErrorMessage(exception);
       
        skipWhileExpression();
    }

    private void buildErrorMessage(ParseException exception) {
        Token token = exception.currentToken;
        Token nextToken = token.next;

        int line = token.beginLine;
        String message = "Encountered '" + nextToken + "' after '" + token + "'";

        this.errorMessages.add(new Report(ReportType.ERROR, Stage.SYNTATIC, line, message));
    }

    public void printErrorMessages() {
        System.out.println("      EXCEPTIONS      ");
        System.out.println("\n#######################\n");

        if(this.errorMessages.size() == 0) System.out.println("None exception relative to while expression!");

        for(int i = 0; i < this.errorMessages.size(); i++)
            System.out.println(this.errorMessages.get(i));
        
        System.out.println("\n#######################\n");
    }
}


PARSER_END(Parser)

SKIP :
{    < "//" (~["\r", "\n"])* >
    | < "/*" > : COMMENTS
	| " " | "\r" | "\t" | "\n"
}

<COMMENTS> SKIP :
{
  < "*/" > : DEFAULT
| < ~[] >
}



TOKEN:
{
        <IF: "if">
    |   <INT: "int">
    |   <NEW: "new">
    |   <VOID: "void">
    |   <ELSE: "else">
    |   <TRUE: "true">
    |   <MAIN: "main">
    |   <THIS: "this">
    |   <WHILE: "while">
    |   <FALSE: "false">
    |   <CLASS: "class">
    |   <PUBLIC: "public">
    |   <STATIC: "static">
    |   <STRING: "String">
    |   <LENGTH: "length">
    |   <RETURN: "return">
    |   <IMPORT: "import">
    |   <BOOLEAN: "boolean">
    |   <EXTENDS: "extends">
    |   <LBRACKET: "{">
    |   <RBRACKET: "}">
    |   <LPAR: "(">
    |   <RPAR: ")">
    |   <LRPAR: "[">
    |   <RRPAR: "]">
    |   <NOT: "!">
    |   <DOT: ".">
    |   <EQUAL: "=">
    |   <VIRG: ",">
    |   <PVIRG: ";">
    |   <AND: "&&">
    |   <LESS: "<">
    |   <PLUS: "+">
    |   <MINUS: "-">
    |   <MULT: "*">
    |   <DIV: "/">
    |   <IDENTIFIER: ((["$"]|["_"]|["A"-"Z"]|["a"-"z"])(["$"]|["0"-"9"]|["A"-"Z"]|["a"-"z"]|["_"])*)>
	|   <INTEGERLITERAL: (["0"-"9"])+ >

}

SimpleNode Program(): {}
{
    Imports() ClassDeclaration() <EOF> {return jjtThis;}
}

void Imports(): {}
{
   (ImportDeclaration())*
}

void ImportDeclaration():{
    Token name;
}
{
    <IMPORT> name = <IDENTIFIER> {jjtThis.put("val", name.image);}  (<DOT> name = <IDENTIFIER> {jjtThis.put("val", jjtThis.get("val") + "." + name.image);})* <PVIRG>
}

void ClassDeclaration() #Class: {
    Token name;
}
{
    <CLASS> name = <IDENTIFIER> {jjtThis.put("val", name.image);} (<EXTENDS> name = <IDENTIFIER> {jjtThis.put("val", jjtThis.get("val") + " " + name.image);})? <LBRACKET>
        (VarDeclaration())*
        (MethodDeclaration())*
    <RBRACKET>
}

void VarDeclaration(): {
    Token id;
}
{
    Type() id = <IDENTIFIER> {jjtThis.put("val", id.image);} <PVIRG>
}

void MethodDeclaration(): {}
{
    <PUBLIC> (RegularMethod() | Main())
}

void RegularMethod(): {
    Token name;
} 
{
    Type() #ReturnType name = <IDENTIFIER> {jjtThis.put("val", name.image);} <LPAR> (MethodParams())? <RPAR> <LBRACKET>
        MethodBody() #MethodBody
        <RETURN> Expression() #ReturnStatement <PVIRG>
    <RBRACKET>
}

void Main(): {} {
    <STATIC> <VOID> <MAIN> <LPAR> <STRING> <LRPAR> <RRPAR> <IDENTIFIER> <RPAR> <LBRACKET>
        MethodBody() #MethodBody
    <RBRACKET>
}

void MethodParams():{}
{
    MethodParam() (<VIRG> MethodParam())*
}

void MethodParam(): {
    Token name;
}
{
    Type() name = <IDENTIFIER> {jjtThis.put("val", name.image);} #VarId(0)
}

void MethodBody() #void: {
    Token type;
}
{
    (LOOKAHEAD(2) VarDeclaration())* (Statement())*
}

void Type() #void: {
    Token type;
}
{
    Type1() | type = <IDENTIFIER> {jjtThis.put("val", type.image);} #Type(0)
}

void Type1() #void:{}
{
    (<INT> {jjtThis.put("val", "int");} (<LRPAR> <RRPAR> {jjtThis.put("val", jjtThis.get("val") + "[]");})?) #Type(0) | <BOOLEAN> {jjtThis.put("val", "boolean");} #Type(0)
}

void Statement() #void: {
    Token name;
}
{
    Scope() | IfAndElse() | While() | ((LOOKAHEAD(3) Expression() | (<IDENTIFIER> ((Assignment()) | (<LRPAR> Expression() <RRPAR> Assignment())))) <PVIRG>)
}

void Scope():{} { <LBRACKET> (Statement())* <RBRACKET>}

void IfAndElse():{} { <IF> IfExpression() IfBody() <ELSE> ElseBody() }
void IfExpression():{} { <LPAR> Expression() <RPAR> }
void IfBody():{} { Statement() }
void ElseBody():{} { Statement() }

void While():{}
{ try{
        <WHILE> WhileExpression()
    }
    catch(ParseException e) {
        handleWhileExpressionError(e);
    }
    WhileBody()
}
void WhileExpression():{} { <LPAR> {countWhileExpressionParetheses = 1;} Expression() <RPAR> {countWhileExpressionParetheses--;} }
void WhileBody():{} { Statement() }

void Assignment():{}{ <EQUAL> Expression() }
void ArrayIndex():{} { <LRPAR> Expression() <RRPAR> }

void Expression(): {} {Expression1() AndExpression()}
void Expression1(): {} {Expression2() LessExpression()}
void Expression2(): {} {Expression3() PlusMinusExpression()}
void Expression3(): {} {Expression4() MultDivExpression()}
void Expression4(): {} {Expression5() DotArrayExpression()}
void Expression5(): {} {(<INTEGERLITERAL> | <TRUE> | <FALSE> | <IDENTIFIER> | <THIS> | (<NEW> ((<INT> <LRPAR> Expression() <RRPAR>) | (<IDENTIFIER> <LPAR> <RPAR>)))
		                | (<NOT> Expression()) | (<LPAR> Expression() <RPAR>))}

void AndExpression(): {} {(<AND> Expression()) | {}}
void LessExpression(): {} {(<LESS> Expression1()) | {}}
void PlusMinusExpression(): {} {((<PLUS> | <MINUS>) Expression2()) | {}}
void MultDivExpression(): {} {((<MULT> | <DIV>) Expression3()) | {}}
void DotArrayExpression(): {} {(<DOT> (<LENGTH> | (<IDENTIFIER> <LPAR> ( Expression()(<VIRG> Expression()))? <RPAR>)) | (<LRPAR> Expression() <RRPAR>)) | {}}
