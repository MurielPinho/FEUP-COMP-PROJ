options
{
    LOOKAHEAD=1;
}

PARSER_BEGIN(Parser)

import java.util.*;
import java.io.*;

import java.io.StringReader;

public class Parser {
    private int whileExpressionErrorsCounter = 0;
    private int MAX_WHILE_EXPRESSION_ERRORS = 300;
    private ArrayList<String> errorMessages = new ArrayList<String>();

    public Parser(String file) throws ParseException, FileNotFoundException{
        // System.out.println("Parsing...");

        System.setIn(new FileInputStream(file));
		Parser parser = new Parser(System.in);
		//SimpleNode root = parser.Program();  // returns reference to root node

		//root.dump(""); // prints the tree on the screen
    }

    /* Skips the while expression until if finds the token "{" */
    public void skipWhileExpression() {
        Token prev; // will store the previous token

        do{
            prev = token;
            token = getNextToken();
        } while(token.kind != LBRACKET && token.kind != EOF);

        if(token.kind == LBRACKET)
            token = prev;
    }

    /* Handles with while expression error */
    public void handleWhileExpressionError(ParseException exception) throws ParseException {
        whileExpressionErrorsCounter++;
        
        if(whileExpressionErrorsCounter > this.MAX_WHILE_EXPRESSION_ERRORS) {
            System.out.println("Maximum Number of While Expression Errors(" + this.MAX_WHILE_EXPRESSION_ERRORS + ") exceeded.");
            this.printErrorMessages();
            throw new ParseException();
        }
        else this.buildErrorMessage(exception);
       
        skipWhileExpression();
    }

    private void buildErrorMessage(ParseException exception) {
        this.errorMessages.add("Error Number "+ whileExpressionErrorsCounter + " -> " + exception);
    }

    private void printErrorMessages() {
        for(int i = 0; i < this.errorMessages.size(); i++)
            System.out.println(this.errorMessages.get(i));
    }
}


PARSER_END(Parser)

SKIP :
{    < "//" (~["\r", "\n"])* >
    | < "/*" > : COMMENTS
	| " " | "\r" | "\t" | "\n"
}

<COMMENTS> SKIP :
{
  < "*/" > : DEFAULT
| < ~[] >
}



TOKEN:
{
        <IF: "if">
    |   <INT: "int">
    |   <NEW: "new">
    |   <VOID: "void">
    |   <ELSE: "else">
    |   <TRUE: "true">
    |   <MAIN: "main">
    |   <THIS: "this">
    |   <WHILE: "while">
    |   <FALSE: "false">
    |   <CLASS: "class">
    |   <PUBLIC: "public">
    |   <STATIC: "static">
    |   <STRING: "String">
    |   <LENGTH: "length">
    |   <RETURN: "return">
    |   <IMPORT: "import">
    |   <BOOLEAN: "boolean">
    |   <EXTENDS: "extends">
    |   <LBRACKET: "{">
    |   <RBRACKET: "}">
    |   <LPAR: "(">
    |   <RPAR: ")">
    |   <LRPAR: "[">
    |   <RRPAR: "]">
    |   <NOT: "!">
    |   <DOT: ".">
    |   <EQUAL: "=">
    |   <VIRG: ",">
    |   <PVIRG: ";">
    |   <AND: "&&">
    |   <LESS: "<">
    |   <PLUS: "+">
    |   <MINUS: "-">
    |   <MULT: "*">
    |   <DIV: "/">
    |   <IDENTIFIER: ((["$"]|["_"]|["A"-"Z"]|["a"-"z"])(["$"]|["0"-"9"]|["A"-"Z"]|["a"-"z"]|["_"])*)>
	|   <INTEGERLITERAL: (["0"-"9"])+ >

}

SimpleNode Program(): {}
{
    Imports() ClassDeclaration() <EOF> {return jjtThis;}
}

void Imports(): {}
{
   (ImportDeclaration())*
}

void ImportDeclaration():{
    Token base, comp;
}
{
    <IMPORT> base = <IDENTIFIER> {jjtThis.val = base.image;}  (<DOT> comp = <IDENTIFIER> {jjtThis.val += "." + comp.image;})* <PVIRG>
}

void ClassDeclaration() #Class: {
    Token name, comp;
}
{
    <CLASS> name = <IDENTIFIER> {jjtThis.val = name.image;} (<EXTENDS> comp = <IDENTIFIER> {jjtThis.val += " " + comp.image;})? <LBRACKET>
        (VarDeclaration())*
        (MethodDeclaration())*
    <RBRACKET>
}

void VarDeclaration(): {}
{
    Type() VarDeclaration1()
}

void VarDeclaration1() #VarId:{
    Token id;
}
{
    id = <IDENTIFIER> {jjtThis.val = id.image;} <PVIRG>
}

void MethodDeclaration(): {}
{
    <PUBLIC> (RegularMethod() | Main())
}

void RegularMethod(): {
    Token name;
} 
{
    Type() #ReturnType name = <IDENTIFIER> {jjtThis.val = name.image;} <LPAR> (MethodParams())? <RPAR> <LBRACKET>
        MethodBody() #MethodBody
        <RETURN> Expression() #ReturnStatement <PVIRG>
    <RBRACKET>
}

void Main(): {} {
    <STATIC> <VOID> <MAIN> <LPAR> <STRING> <LRPAR> <RRPAR> <IDENTIFIER> <RPAR> <LBRACKET>
        MethodBody() #MethodBody
    <RBRACKET>
}

void MethodParams():{}
{
    MethodParam() (<VIRG> MethodParam())*
}

void MethodParam(): {
    Token name;
}
{
    Type() name = <IDENTIFIER> {jjtThis.val = name.image;} #VarId(0)
}

void MethodBody() #void: {
    Token type;
}
{
    (
        (type = <IDENTIFIER> {jjtThis.val = type.image;} #Type(0) ((VarDeclaration1() #VarDeclarartion(2) MethodBody())
        | (Statement2() (Statement())*)))
        | (Type1() VarDeclaration1() #VarDeclarartion(2) MethodBody())
        | (Statement1() (Statement())*)
    )?
}

void Type() #void: {
    Token type;
}
{
    Type1() | type = <IDENTIFIER> {jjtThis.val = type.image;} #Type(0)
}

void Type1() #void:{}
{
    (<INT> {jjtThis.val = "int";} (<LRPAR> <RRPAR> {jjtThis.val = "[]";})?) #Type(0) | <BOOLEAN> {jjtThis.val = "boolean";} #Type(0)
}

void Statement() #void: {
    Token name;
}
{
    Statement1()
    | (name = <IDENTIFIER> {jjtThis.val = name.image;} Statement2()) #Var
}

void Statement1() #void:{}
{
    Scope()
    | IfAndElse()
    | While()
    | (Expression3() Expression1() <PVIRG>)
}

void Statement2() #void:{}
{
    Assignment()
    | (ArrayIndex() (Assignment())?) 
    | (Expression4() <PVIRG>)
}

void Assignment():{}
{
    <EQUAL> Expression() <PVIRG>
}

void Scope():{}
{
    <LBRACKET> (Statement())* <RBRACKET>
}

void IfAndElse():{}
{
    <IF> IfExpression() IfBody() <ELSE> ElseBody()
}

void IfExpression():{}
{
    <LPAR> Expression() <RPAR>
}

void IfBody():{}
{
    Statement()
}

void ElseBody():{}
{
    Statement()
}

void While():{}
{
    try{
        <WHILE> WhileExpression()
    }
    catch(ParseException e) {
        handleWhileExpressionError(e);
    }
    WhileBody()
}

void WhileExpression():{}
{
    <LPAR> Expression() <RPAR>
}

void WhileBody():{}
{
    Statement()
}

void ArrayIndex():{}
{
    <LRPAR> Expression() <RRPAR>
}

void Expression() #void: {}
{
    Expression2() Expression1()
}

void Expression1() #void:{}
{
    (Expression4()
    | ArrayIndex())?
}

void Expression2() #void:{
  Token name;
}
{
    Expression3() | name = <IDENTIFIER> {jjtThis.val = name.image;} #Var(0)
}

void Expression3() #void:{
  Token name;
}
{
    name = <INTEGERLITERAL> {jjtThis.val = name.image;} #IntegerLiteral(0)
    | <TRUE> #True(0)
    | <FALSE> #False(0)
    | <THIS> #This(0)
    | (<NEW> ((name = <IDENTIFIER> {jjtThis.val = name.image;} <LPAR> <RPAR>) #ConstructorClass(0)
             |(<INT> <LRPAR> Expression() <RRPAR> #ConstructorIntArray)))
    | (<NOT> Expression() #Not(2))
    | (<LPAR> Expression() <RPAR>) #SubExpression
}

void Expression4() #void:{}
{
    ((<AND> #And(0)
     |<LESS> #Less(0)
     |<PLUS> #Plus(0)
     |<MINUS> #Minus(0)
     |<MULT> #Mult(0)
     |<DIV> #Div(0)) Expression())
    | (<DOT> ((<LENGTH>) #Length(0) | MethodInvocation()))
}

void MethodInvocation():{
    Token name;
}
{
    name = <IDENTIFIER> {jjtThis.val = name.image;} MethodArgs()
}

void MethodArgs():{}
{
    <LPAR> (MethodArg() (<VIRG> MethodArg())*)? <RPAR>
}

void MethodArg():{}
{
    Expression()
}
